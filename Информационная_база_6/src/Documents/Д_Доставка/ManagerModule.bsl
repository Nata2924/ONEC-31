#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

Процедура ПриОпределенииНастроекПечати(НастройкиОбъекта) Экспорт	
	НастройкиОбъекта.ПриДобавленииКомандПечати = Истина;
КонецПроцедуры

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Транспортная накладная
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТранспортнаяНакладная";
	КомандаПечати.Представление = НСтр("ru = 'Транспортная накладная'");
	КомандаПечати.Порядок = 5;
	
	// Анкета клиента
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АнкетаКлиента";
	КомандаПечати.Представление = НСтр("ru = 'Анкета клиента'");
	КомандаПечати.Порядок = 10;
	
	// Договор (docx)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.Д_Доставка";
	КомандаПечати.Идентификатор = "ДоговорНаДоставку";
	КомандаПечати.Представление = НСтр("ru = 'Договор на доставку'");
	КомандаПечати.Порядок = 15;
	
	// Комплект документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТранспортнаяНакладная,АнкетаКлиента";
	КомандаПечати.Представление = НСтр("ru = 'Комплект документов'");
	КомандаПечати.Порядок = 75;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "ТранспортнаяНакладная");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьТранспортнойНакладной(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Транспортная накладная'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.Д_Доставка.ПФ_MXL_ТранспортнаяНакладная";
	КонецЕсли;
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "АнкетаКлиента");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьАнкетыКлиента(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Анкета клиента'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.Д_Доставка.ПФ_MXL_АнкетаКлиента";
	КонецЕсли;
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "ДоговорНаДоставку");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ОфисныеДокументы = ПечатьДоговораНаДоставку(МассивОбъектов);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Договор на доставку'");
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПечатьТранспортнойНакладной(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ТранспортнаяНакладная";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.Д_Доставка.ПФ_MXL_ТранспортнаяНакладная");
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВывестиЗаголовок(ДанныеДокументов, ТабличныйДокумент, Макет, "Транспортная накладная");

		ВывестиТоварыУслуги(ДанныеДокументов, ТабличныйДокумент, Макет);

		ВывестиПодвалТранспортнойНакладной(ДанныеДокументов, ТабличныйДокумент, Макет);
		
        // В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);		
		
	КонецЦикла;	
		
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПечатьАнкетыКлиента(МассивОбъектов, ОбъектыПечати)
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_АнкетаКлиента";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.Д_Доставка.ПФ_MXL_АнкетаКлиента");
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВывестиЗаголовок(ДанныеДокументов, ТабличныйДокумент, Макет, "Анкета о доставке");
		
		ВывестиОценкуТовара(ТабличныйДокумент, Макет);
		
		ВывестиКомментарий(ТабличныйДокумент, Макет);
		
		ВывестиОценкуДоставки(ТабличныйДокумент, Макет);
		
		ВывестиКомментарий(ТабличныйДокумент, Макет);
		
		ВывестиРекомендацию(ТабличныйДокумент, Макет);
        // В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);		
		
	КонецЦикла;	
		
	Возврат ТабличныйДокумент;
КонецФункции

Функция ПолучитьДанныеДокументов(МассивОбъектов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Доставка.Ссылка КАК Ссылка,
	               |	Доставка.Номер КАК Номер,
	               |	Доставка.Дата КАК Дата,
	               |	Доставка.Д_Организация КАК Организация,
	               |	Доставка.Д_Контрагент КАК Контрагент,
	               |	Доставка.Д_Договор КАК Договор,
	               |	Доставка.Д_АдресДоставки КАК АдресДоставки,
	               |	Доставка.Д_Основание КАК Основание,
	               |	Доставка.Д_Ответственный КАК Ответственный,
	               |	Доставка.Д_Комментарий КАК Комментарий,
	               |	Доставка.Д_Товары.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Д_Номенклатура КАК Номенклатура,
	               |		Д_Количество КАК Количество
	               |	) КАК Товары,
	               |	РеализацияТоваровУслуг.СуммаДокумента КАК СуммаДокумента,
	               |	РеализацияТоваровУслуг.Д_КонтактноеЛицо КАК КонтактноеЛицо,
	               |	Доставка.Д_Контрагент.ИНН КАК Д_КонтрагентИНН,
	               |	Доставка.Д_Контрагент.КПП КАК Д_КонтрагентКПП,
	               |	РеализацияТоваровУслуг.Д_КонтактноеЛицо КАК Д_КонтактноеЛицо,
	               |	РеализацияТоваровУслуг.Контрагент.ЮридическийАдрес КАК КонтрагентЮридическийАдрес,
	               |	Доставка.Д_Контрагент.ЮридическийАдрес КАК Д_КонтрагентЮридическийАдрес
	               |ИЗ
	               |	Документ.Д_Доставка КАК Доставка
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	               |		ПО Доставка.Д_Основание = РеализацияТоваровУслуг.Ссылка
	               |ГДЕ
	               |	Доставка.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

Функция ПечатьДоговораНаДоставку(МассивОбъектов)
	
	ОфисныеДокументы = Новый Соответствие;
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.Д_Доставка.ПФ_DOC_ДоговорНаДоставку");
	
	МакетОфисный = УправлениеПечатью.ИнициализироватьМакетОфисногоДокумента(Макет, Неопределено);
	
	ОписаниеОбластей = Новый Структура;
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Шапка", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Тело", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Подвал", "Общая");
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
	
	Пока ДанныеДокументов.Следующий() Цикл
		ПечатнаяФорма = УправлениеПечатью.ИнициализироватьПечатнуюФорму(Неопределено, Неопределено, МакетОфисный);
		ЗначенияПараметров = Новый Структура;

		ЗначенияПараметров.Вставить("Дата", ДанныеДокументов.Дата);
		ЗначенияПараметров.Вставить("Организация", ДанныеДокументов.Организация);
		ЗначенияПараметров.Вставить("Ответственный", ДанныеДокументов.Ответственный);
		ЗначенияПараметров.Вставить("Контрагент", ДанныеДокументов.Контрагент);
		ЗначенияПараметров.Вставить("СуммаДок", ДанныеДокументов.СуммаДокумента);
		ЗначенияПараметров.Вставить("АдресДоставки", ДанныеДокументов.АдресДоставки);
		ЗначенияПараметров.Вставить("КонтактноеЛицо", ДанныеДокументов.КонтактноеЛицо);
		ЗначенияПараметров.Вставить("ИНН", ДанныеДокументов.Д_КонтрагентИНН);
		ЗначенияПараметров.Вставить("КПП", ДанныеДокументов.Д_КонтрагентКПП);
		ЗначенияПараметров.Вставить("ЮридическийАдрес", ДанныеДокументов.КонтрагентЮридическийАдрес); 
		
		Область = УправлениеПечатью.ОбластьМакета(МакетОфисный, ОписаниеОбластей.Шапка);
		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ЗначенияПараметров);
		
		Область = УправлениеПечатью.ОбластьМакета(МакетОфисный, ОписаниеОбластей.Тело);
		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ЗначенияПараметров);
		
		Область = УправлениеПечатью.ОбластьМакета(МакетОфисный, ОписаниеОбластей.Подвал);
		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ЗначенияПараметров);
		
		АдресХранилищаПечатнойФормы = УправлениеПечатью.СформироватьДокумент(ПечатнаяФорма);
		
		ОфисныеДокументы.Вставить(АдресХранилищаПечатнойФормы, Строка(ДанныеДокументов.Ссылка));
		УправлениеПечатью.ОчиститьСсылки(ПечатнаяФорма);
	КонецЦикла;
	УправлениеПечатью.ОчиститьСсылки(МакетОфисный);
	
	Возврат	ОфисныеДокументы;
КонецФункции

Процедура ВывестиЗаголовок(ДанныеДокументов, ТабличныйДокумент, Макет, Шаблон)
	
	ОбластьЗаголовокДокумента = Макет.ПолучитьОбласть("Заголовок");
	
	ДанныеПечати = Новый Структура;
	
	Если Шаблон = "Транспортная накладная" Тогда
		ШаблонЗаголовка = "Транспортная накладная №%1 от %2";
	ИначеЕсли Шаблон = "Анкета о доставке" Тогда
		ШаблонЗаголовка = "Анкета о доставке №%1 от %2";
	КонецЕсли;
	
	ТекстЗаголовка = СтрШаблон(ШаблонЗаголовка,
		ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокументов.Номер),
		Формат(ДанныеДокументов.Дата, "ДЛФ=DD"));
	ДанныеПечати.Вставить("ТекстЗаголовка", ТекстЗаголовка);
	
	ДанныеПечати.Вставить("Грузоотправитель", ДанныеДокументов.Организация);
	ДанныеПечати.Вставить("Грузополучатель", ДанныеДокументов.Контрагент);
	
	ОбластьЗаголовокДокумента.Параметры.Заполнить(ДанныеПечати);
	
	НавСсылка = ПолучитьНавигационнуюСсылку(ДанныеДокументов.Ссылка);
	ДанныеQRКода = ГенерацияШтрихкода.ДанныеQRКода(НавСсылка, 1, 120);
	
	Если НЕ ТипЗнч(ДанныеQRКода) = Тип("ДвоичныеДанные") Тогда
	    ТекстСообщения = НСтр("ru = 'Не удалось сформировать QR-код.
	                |Технические подробности см. в журнале регистрации.'");
	    ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	Иначе
	    КартинкаQRКода = Новый Картинка(ДанныеQRКода);
	    ОбластьЗаголовокДокумента.Рисунки.QRCode.Картинка = КартинкаQRКода;
	КонецЕсли;		
	
	ТабличныйДокумент.Вывести(ОбластьЗаголовокДокумента);
	
КонецПроцедуры

Процедура ВывестиТоварыУслуги(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока = Макет.ПолучитьОбласть("СтрокаТаблицы");
	
	ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ВыборкаТовары = ДанныеДокументов.Товары.Выбрать();
	Пока ВыборкаТовары.Следующий() Цикл
		ОбластьСтрока.Параметры.Заполнить(ВыборкаТовары);
		ТабличныйДокумент.Вывести(ОбластьСтрока);
	КонецЦикла;
		
КонецПроцедуры

Процедура ВывестиПодвалТранспортнойНакладной(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьПодвалЗаказа = Макет.ПолучитьОбласть("ОбластьПодвал");

	ТабличныйДокумент.Вывести(ОбластьПодвалЗаказа);
	
КонецПроцедуры

Процедура ВывестиОценкуТовара(ТабличныйДокумент, Макет)
	
	Область = Макет.ПолучитьОбласть("ОценкаТовара");

	ТабличныйДокумент.Вывести(Область);
	
КонецПроцедуры

Процедура ВывестиКомментарий(ТабличныйДокумент, Макет)
	
	Область = Макет.ПолучитьОбласть("Комментарий");

	ТабличныйДокумент.Вывести(Область);
	
КонецПроцедуры

Процедура ВывестиОценкуДоставки(ТабличныйДокумент, Макет)
	
	Область = Макет.ПолучитьОбласть("ОценкаДоставки");

	ТабличныйДокумент.Вывести(Область);
	
КонецПроцедуры

Процедура ВывестиРекомендацию(ТабличныйДокумент, Макет)
	
	Область = Макет.ПолучитьОбласть("Рекомендация");

	ТабличныйДокумент.Вывести(Область);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли